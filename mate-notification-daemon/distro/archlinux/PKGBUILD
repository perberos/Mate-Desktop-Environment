pkgname=mate-notification-daemon
pkgver=2011.11.16
pkgrel=1
pkgdesc="Notification daemon for MATE"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('libmatenotify' 'libwnck' 'gtk2')
makedepends=('intltool')
conflicts=('notification-daemon')
provides=('notification-daemon')
url="http://matsusoft.com.ar/projects"
groups=('mate')
options=('!libtool' '!emptydirs')
install=mate-notification-daemon.install
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('36db424b077488a5786fedf5ea8d15d2c93656b94ba99de0db607e7ceb280767')

build() {
    cd "${srcdir}/${pkgname}"

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/mate-notification-daemon \
        --disable-schemas-install || return 1

    make || return 1
}

package() {
    cd "${srcdir}/${pkgname}"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    # hax! still conflicts with libnotify
    #ln -s mate-notify-send ${pkgdir}/usr/bin/notify-send

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
