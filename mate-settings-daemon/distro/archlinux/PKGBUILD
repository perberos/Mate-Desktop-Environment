pkgname=mate-settings-daemon
pkgver=2011.11.16
pkgrel=2
pkgdesc="The MATE Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatekbd' 'mate-desktop' 'libmatenotify' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
options=('!libtool')
url="http://matsusoft.com.ar/projects"
install=mate-settings-daemon.install
groups=('mate')
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('0b2d059f5c7d15e4ac74197b380e59854a9450f9cb78fa42643f15961012fd5e')

build() {
    cd "$srcdir/$pkgname"
    # Remove --disable-pulse if you want pulseaudio support
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/bin/${pkgname} \
        --disable-static \
        --disable-pulse \
        --disable-schemas-install \
        --enable-polkit || return 1
    make || return 1
}

package() {
    cd "${srcdir}/${pkgname}"

    make DESTDIR="${pkgdir}" MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

    install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
