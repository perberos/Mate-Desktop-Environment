pkgname=mate-keyring
pkgver=2011.10.29
pkgrel=1
pkgdesc="MATE Password Management daemon"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('mate-conf')
makedepends=('intltool' 'pkgconfig' 'python2')
url="http://matsusoft.com.ar/projects"
groups=('mate')
options=('!libtool' '!emptydirs')
install=mate-keyring.install
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('a8c7908bfcb5cca5ca6245b9567ede88d65651553372cd250155c706442a6c1f')

build() {
    cd "${srcdir}/${pkgname}"

    ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
        --libexecdir=/usr/lib/mate-keyring \
        --with-pam-dir=/lib/security --with-root-certs=/etc/ssl/certs || return 1

    make || return 1
}

package() {
    cd "${srcdir}/${pkgname}"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
