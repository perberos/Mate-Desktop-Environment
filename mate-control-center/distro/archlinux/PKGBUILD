pkgname=mate-control-center
pkgver=2011.9.26
pkgrel=1
pkgdesc="The Control Center for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-window-manager' 'libxss' 'desktop-file-utils' 'mate-settings-daemon' 'libmatekbd' 'libcanberra' 'sound-theme-freedesktop' 'libunique' 'mate-menus' 'librsvg') #  'evolution-data-server>=2.29.92'
makedepends=('mate-doc-utils' 'pkgconfig' 'intltool')
options=('!emptydirs' '!libtool')
url="http://matsusoft.com.ar/projects"
groups=('mate')
install=mate-control-center.install
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('191b0eda67941b93f3542b1dfcfd16cc6823a8304eb56c22921a73ac3a079adf')

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--disable-scrollkeeper --disable-update-mimedb || return 1
	#  --enable-aboutme <- this requires libebook (from evolution)
	make || return 1
}

package() {
	cd "${srcdir}/${pkgname}"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas

	# --disable-update-mimedb?
	rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
